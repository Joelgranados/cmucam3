/*
 * Copyright 2006  Anthony Rowe and Adam Goode
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */


/*  Linker Script File    */

ENTRY(_boot)
STACK_SIZE = 0x2400;

MEMORY
{
  ROM (rx) : ORIGIN = 0x00000000, LENGTH = 120K
  RAM (rw) : ORIGIN = 0x40000000, LENGTH = 64K
}

/* Section Definitions */
SECTIONS
{
  /* .text in ROM which is used for code */
  .text :
  {
    KEEP(*(.init))             /* Startup code from .init-section */
    *(.text)
    *(.text.*)
    *(.gnu.linkonce.t.*)
    *(.glue_7)
    *(.glue_7t)
    *(.gcc_except_table)
    *(.gnu.linkonce.r.*)
    *(.rodata)                 /* read-only data (constants) */
    *(.rodata*)

    PROVIDE (etext = .);
  } > ROM

  __exidx_start = .;
  .ARM.exidx :
  {
    *(.ARM.exidx* .gnu.linkonce.armexidx.*)
    _etext = . ;               /* IMPORTANT! used by startup.s */
  } > ROM
  __exidx_end = .;


  /* first in RAM is the stack, which grows down (ultimately past RAM where
     it can trigger a data abort on overflow) */
  .stack :
  {
    . += STACK_SIZE;
    _stack = .;
  } > RAM


  /* .data section which is used for initialized data
     (takes space in both ROM and RAM) */
  .data :
  {
    _data = .;
    *(.data)
    *(.data.*)
    *(.gnu.linkonce.d*)
    *(.fastrun)

    _edata = . ;
    PROVIDE (edata = .);
  } > RAM  AT > ROM


  /* .bss section which is used for uninitialized data (just in RAM) */
  .bss (NOLOAD) :
  {
    __bss_start = . ;
    __bss_start__ = . ;
    *(.bss)
    *(.bss.*)
    *(.gnu.linkonce.b*)
    *(COMMON)

    __bss_end__ = . ;
    PROVIDE (__bss_end = .);
  } > RAM


  /* now, mark the end of static memory */
  _end = . ;
  PROVIDE (end = .);

  /* mark the end of the heap */
  _heap_end = ORIGIN(RAM) + LENGTH(RAM);
  PROVIDE (heap_end = ORIGIN(RAM) + LENGTH(RAM));


  /* Stabs debugging sections.  */
  .stab          0 : { *(.stab) }
  .stabstr       0 : { *(.stabstr) }
  .stab.excl     0 : { *(.stab.excl) }
  .stab.exclstr  0 : { *(.stab.exclstr) }
  .stab.index    0 : { *(.stab.index) }
  .stab.indexstr 0 : { *(.stab.indexstr) }
  .comment       0 : { *(.comment) }


  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1 */
  .debug          0 : { *(.debug) }
  .line           0 : { *(.line) }
  /* GNU DWARF 1 extensions */
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  /* DWARF 1.1 and DWARF 2 */
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  /* DWARF 2 */
  .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
  .debug_abbrev   0 : { *(.debug_abbrev) }
  .debug_line     0 : { *(.debug_line) }
  .debug_frame    0 : { *(.debug_frame) }
  .debug_str      0 : { *(.debug_str) }
  .debug_loc      0 : { *(.debug_loc) }
  .debug_macinfo  0 : { *(.debug_macinfo) }
  /* SGI/MIPS DWARF 2 extensions */
  .debug_weaknames 0 : { *(.debug_weaknames) }
  .debug_funcnames 0 : { *(.debug_funcnames) }
  .debug_typenames 0 : { *(.debug_typenames) }
  .debug_varnames  0 : { *(.debug_varnames) }
}
